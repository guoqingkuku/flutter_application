plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32


    //读取properties文件
    def properties = new Properties()
    properties.load(project.rootProject.file('keys.properties').newDataInputStream())

    //获取project task
    def flutterArrDebug = task('flutterArrDebug', type: Exec) {
        workingDir '../flutter_module'
        commandLine 'flutter', 'build', 'aar', '--no-profile', '--no-release'
    }
    def flutterArrRelease = task('flutterArrRelease', type: Exec) {
        workingDir '../flutter_module'
        commandLine 'flutter', 'build', 'aar', '--no-profile', '--no-debug'
    }


    defaultConfig {
        applicationId "com.clx.flutter_application"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'android'
            keyPassword properties['keyPassword']
            storeFile file(properties['storeFile'])
            storePassword properties['storePassword']
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
            preBuild.dependsOn flutterArrDebug
        }
        release {
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
            preBuild.dependsOn flutterArrRelease
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    flavorDimensions "env"

    productFlavors {
        dev {
            dimension "env"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        prod {
            dimension "env"
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
        }
    }


}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    debugImplementation 'com.example.flutter_module:flutter_debug:1.0'
    releaseImplementation 'com.example.flutter_module:flutter_release:1.0'

}